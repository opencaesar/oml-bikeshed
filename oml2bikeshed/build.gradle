buildscript {
	repositories {
	    mavenLocal()
  		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:1.0.21'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

repositories {
	mavenLocal()
	maven { url 'https://dl.bintray.com/opencaesar/oml' }
	mavenCentral()
	jcenter()
}

apply plugin: 'java'
apply plugin: 'org.xtext.xtend'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.jfrog.bintray'

group = 'io.opencaesar.bikeshed'
version = '0.7.3'
mainClassName = 'io.opencaesar.oml2bikeshed.App'
applicationName = 'oml2bikeshed'
ext.title = 'Oml to Bikeshed'
description = 'Bikeshed generator for Oml'

apply from: "${rootDir}/gradle/source-layout.gradle"
apply from: "${rootDir}/gradle/maven-deployment.gradle"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations.all {
	exclude group: 'asm'
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.beust:jcommander:1.72'
    implementation "io.opencaesar.oml:io.opencaesar.oml.dsl:0.7.+"
	implementation 'net.sourceforge.plantuml:plantuml:1.2019.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

task createVersion {
    doLast {
        def version = project.version.toString()
        def file = new File("$projectDir/src/main/resources/version.txt")
        file.write(version)
    }
}

classes {
    dependsOn createVersion
}

gradlePlugin {
    plugins {
        oml2BikeshedPlugin {
            id = 'io.opencaesar.oml2bikeshed'
            implementationClass = 'io.opencaesar.oml2bikeshed.GradlePlugin'
        }
    }
}

