buildscript {
	repositories {
	    mavenLocal()
  		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:1.0.21'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

repositories {
	mavenLocal()
	maven { url 'https://dl.bintray.com/opencaesar/oml-server' }
	mavenCentral()
	jcenter()
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'org.xtext.xtend'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

group = 'io.opencaesar.bikeshed'
version = '0.2.0'

ext.title = 'Oml to Bikeshed'
description = 'Bikeshed generator for Oml'
mainClassName = 'io.opencaesar.oml2bikeshed.App'

apply from: "${rootDir}/gradle/source-layout.gradle"
apply from: "${rootDir}/gradle/maven-deployment.gradle"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations.all {
	exclude group: 'asm'
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Legacy OML Converter',
				'Implementation-Version': version
	}
	// also write version to properties file for access in prior to deployment
	def props = new Properties()
	props.setProperty('build.version', version)
	def propertyFile = file "src/main/resources/application.properties"
	propertyFile.withWriter { props.store(it, "set in gradle build" )}
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.beust:jcommander:1.72'
    implementation "io.opencaesar.oml:io.opencaesar.oml.dsl:0.3.2+"
	implementation 'net.sourceforge.plantuml:plantuml:1.2019.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}
